PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX oslc: <http://open-services.net/ns/core#>
PREFIX cogs: <http://vocab.deri.ie/cogs#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX meb: <http://rdf.myexperiment.org/ontologies/base/>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?attachment a nfo:FileDataObject;
      ?attachmentP ?attachmentO.
  }
}
INSERT {
  GRAPH ?orgG {
    ?attachment a nfo:FileDataObject;
      ?attachmentP ?attachmentO.
  }
}
WHERE {
  GRAPH ?orgG {
    ?subDoc a <http://lblod.data.gift/vocabularies/automatische-melding/FormData>;
      dct:hasPart ?attachment.
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?attachment a nfo:FileDataObject;
      ?attachmentP ?attachmentO.
  }
}
;
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?pFile nie:dataSource ?attachment;
      ?pFileP ?pFileO.
  }
}
INSERT {
  GRAPH ?orgG {
    ?pFile nie:dataSource ?attachment;
      ?pFileP ?pFileO.
  }
}
WHERE {
  GRAPH ?orgG {
    ?subDoc a <http://lblod.data.gift/vocabularies/automatische-melding/FormData>;
      dct:hasPart ?attachment.
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?pFile nie:dataSource ?attachment;
      ?pFileP ?pFileO.
  }
}
