PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

DELETE {
  GRAPH ?h {
    ?bestuurseenheid
      lblodSubsidie:drawingRight ?drawingRight ;
      dbpedia:populationTotal ?populationTotal .
  }
}
INSERT {
  GRAPH ?g {
    <http://lblod.data.info/id/subsidy-measure-offers/3ed41394-af3f-42e5-8377-33957874501d>
      m8g:hasCriterion ?criterion ;
      subsidie:biedtAan ?subsidyAmount ;
      lblodSubsidie:heeftReeks  <http://lblod.data.info/id/subsidy-measure-offer-series/811ea781-c0dc-4647-ad8f-ea090bafc61d> .

  
    <http://data.lblod.info/id/criteria/e75fa961-8135-49c8-acd9-ce62221842dd> a m8g:Criterium ;
      mu:uuid "e75fa961-8135-49c8-acd9-ce62221842dd" ;
      dct:title "Toepasbaar op gemeentebesturen" ;
      dct:isPartOf <http://data.lblod.info/id/subsidy-procedural-steps/f23837f6-8d0e-49f8-b923-79cf5a010cb4> ;
      m8g:fulfilledByRequirementGroup ?requirementGroup .
  }
  GRAPH ?h {
    ?subsidyAmount a subsidie:Subsidiebebdrag ;
      mu:uuid ?subsidyAmountUuid ;
      subsidie:maximumbedrag ?drawingRight ;
      dct:requires ?requirementGroup .

    ?requirementGroup a m8g:RequirementGroup ;
      mu:uuid ?requirementGroupUuid ;
      dct:description ?requirementGroupDescription ;
      m8g:hasCriterionRequirement ?criterionRequirementBestuurseenheid ;
      m8g:hasCriterionRequirement ?criterionRequirementPopulation .

    ?criterionRequirementBestuurseenheid a m8g:CriterionRequirement ;
      mu:uuid ?criterionRequirementBestuurseenheidUuid ;
      dct:description "Het gemeentebestuur van toepassing" ;
      lblodSubsidie:isSatisfiableBy ?bestuurseenheid .

    ?criterionRequirementPopulation a m8g:CriterionRequirement ;
      mu:uuid ?criterionRequirementPopulationUuid ;
      dct:description "Het vereiste bevolkingsaantal" ;
      dbpedia:populationTotal ?populationTotal .
  }
} WHERE {
  GRAPH ?g {
    ?bestuurseenheid a besluit:Bestuurseenheid ;
      skos:prefLabel ?labelBestuurseenheid .
  }
  GRAPH ?h {
    ?bestuurseenheid
      lblodSubsidie:drawingRight ?drawingRight ;
      dbpedia:populationTotal ?populationTotal .
  }

  BIND(SHA256(CONCAT(STR(?populationTotal), "subsidyAmountClimate2")) as ?subsidyAmountUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/subsidy-amounts/", ?subsidyAmountUuid)) AS ?subsidyAmount) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "requirementGroupClimate2")) as ?requirementGroupUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/requirement-groups/", ?requirementGroupUuid)) AS ?requirementGroup) .
  BIND(CONCAT("Vereistengroup voor klimaatsubsidies - ", STR(?labelBestuurseenheid)) as ?requirementGroupDescription) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "criterionRequirementBestuurseenheidClimate2")) as ?criterionRequirementBestuurseenheidUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/criterion-requirements/", ?criterionRequirementBestuurseenheidUuid)) AS ?criterionRequirementBestuurseenheid) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "criterionRequirementPopulationClimate2")) as ?criterionRequirementPopulationUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/criterion-requirements/", ?criterionRequirementPopulationUuid)) AS ?criterionRequirementPopulation) .
}