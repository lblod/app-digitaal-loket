PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX oslc: <http://open-services.net/ns/core#>
PREFIX cogs: <http://vocab.deri.ie/cogs#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX meb: <http://rdf.myexperiment.org/ontologies/base/>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX schema: <http://schema.org/>
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?vFile a nfo:FileDataObject;
      ?vFileP ?vFileO.

    ?pFile a nfo:FileDataObject;
      ?pFileP ?pFileO;
      nie:dataSource ?vFile.
  }
}
INSERT {

  GRAPH ?orgGraph {
    ?vFile a nfo:FileDataObject;
      ?vFileP ?vFileO.

    ?pFile a nfo:FileDataObject;
      ?pFileP ?pFileO;
      nie:dataSource ?vFile.
  }

}
WHERE {

  GRAPH ?orgGraph {
     ?form a lblodSubsidie:ApplicationForm;
       ( lblodSubsidie:decisionUpload | lblodSubsidie:reportUpload | schema:bankAccount | lblodSubsidie:picturesUpload | lblodSubsidie:attachment | lblodSubsidie:signedPact)/dct:hasPart ?vFile.
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?vFile a nfo:FileDataObject;
      ?vFileP ?vFileO.

    ?pFile a nfo:FileDataObject;
      ?pFileP ?pFileO;
      nie:dataSource ?vFile.
  }
}