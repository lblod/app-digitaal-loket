PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX oslc: <http://open-services.net/ns/core#>
PREFIX cogs: <http://vocab.deri.ie/cogs#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX meb: <http://rdf.myexperiment.org/ontologies/base/>

## START - Moving cached files from remote data objects
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?remoteDataObject a nfo:RemoteDataObject.
    ?remoteDataObject ?remoteDataObjectP ?remoteDataObjectO.
  }
}
INSERT {
  GRAPH ?orgG {
    ?remoteDataObject a nfo:RemoteDataObject.
    ?remoteDataObject ?remoteDataObjectP ?remoteDataObjectO.
  }
}
WHERE {
  GRAPH ?orgG {
    ?submission a <http://lblod.data.gift/vocabularies/automatische-melding/FormData>;
      <http://purl.org/dc/terms/hasPart> ?remoteDataObject.
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?remoteDataObject a nfo:RemoteDataObject.
    ?remoteDataObject ?remoteDataObjectP ?remoteDataObjectO.
  }
}
;
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?htmlFile nie:dataSource ?remoteDataObject.
    ?htmlFile ?htmlFileP ?htmlFileO.
  }
}
INSERT {
  GRAPH ?orgG {
    ?htmlFile nie:dataSource ?remoteDataObject.
    ?htmlFile ?htmlFileP ?htmlFileO.
  }
}
WHERE {
  GRAPH ?orgG {
    ?submission a <http://lblod.data.gift/vocabularies/automatische-melding/FormData>;
    <http://purl.org/dc/terms/hasPart> ?remoteDataObject.
    ?remoteDataObject a nfo:RemoteDataObject.
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?htmlFile nie:dataSource ?remoteDataObject.
    ?htmlFile ?htmlFileP ?htmlFileO.
  }
}
## END - Moving RemoteDataObject and its related html and ttl files