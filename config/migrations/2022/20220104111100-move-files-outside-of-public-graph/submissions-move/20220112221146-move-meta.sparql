PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX oslc: <http://open-services.net/ns/core#>
PREFIX cogs: <http://vocab.deri.ie/cogs#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX meb: <http://rdf.myexperiment.org/ontologies/base/>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?source a nfo:FileDataObject;
      ?sourceP ?sourceO.
  }
}
INSERT {
  GRAPH ?orgG {
    ?source a nfo:FileDataObject;
      ?sourceP ?sourceO.
  }
}
WHERE {
  GRAPH ?orgG {
    ?subDoc a ext:SubmissionDocument;
      dct:source ?source.
  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?source a nfo:FileDataObject;
      <http://purl.org/dc/terms/type> <http://data.lblod.gift/concepts/meta-file-type>;
      ?sourceP ?sourceO.
  }
}