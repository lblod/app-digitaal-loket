PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX adms: <http://www.w3.org/ns/adms#>

DELETE {
    GRAPH ?g {
        ?submission
            # NOTE: status sent
            adms:status <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484>.
    }
}
INSERT {
    GRAPH ?g {
        ?submission
            # NOTE: status active
            adms:status <http://lblod.data.gift/concepts/c849ca98-455d-4f31-9e95-a3d9d06e4497>;
            # NOTE: third and final step in the flow
            common:active <http://lblod.data.info/id/subsidie-application-flow-steps/85e66a24-4bf5-4cff-ac70-6af49e29e693>.
    }
}
WHERE {
    GRAPH ?g {
        ?submission
            a subsidie:SubsidiemaatregelConsumptie;
            # NOTE: status sent
            adms:status <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484>;
            # NOTE: current active flow
            cpsv:follows <http://lblod.data.info/id/subsidie-application-flows/6d850270-a84b-466d-a572-84a17a734267>.
    }
}
