PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX adms: <http://www.w3.org/ns/adms#>

DELETE {
    GRAPH ?g {
        ?consumption
            # NOTE: status sent
            adms:status <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484>;
            common:active ?active.
    }
}
INSERT {
    GRAPH ?g {
        ?consumption
            # NOTE: status active
            adms:status <http://lblod.data.gift/concepts/c849ca98-455d-4f31-9e95-a3d9d06e4497>;
            # NOTE: third and final step in the flow
            common:active <http://lblod.data.info/id/subsidie-application-flow-steps/08598c1d-1ec8-4ab4-88f8-3623d7952c98>.
    }
}
WHERE {
    GRAPH ?g {
        ?consumption
            a subsidie:SubsidiemaatregelConsumptie;
            # NOTE: status sent
            adms:status <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484>;
            # NOTE: old flow
            cpsv:follows <http://lblod.data.info/id/subsidie-application-flows/1a7fb92ca5e7a948c687bda5754233cbb654d1c324dda5dbe9fce6ed4fbad163>.
        OPTIONAL {
            # NOTE: in some rare cases, this is still set on submitted flows
            ?consumption common:active ?active.
        }
    }
}