PREFIX dct: <http://purl.org/dc/terms/>
PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>
PREFIX pav: <http://purl.org/pav/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX rlog: <http://persistence.uni-leipzig.org/nlp2rdf/ontologies/rlog#>

# In this file we delete triples linked to certain data types for which the data
# becomes obsolete after some time.
# As there are recent occurences of those types, we assume they are still in use
# and we shouldn't delete all of them. It's why we remove the ones from the past years.

# Delete DownloadEvent than a month, based on dct:modified

DELETE {
  GRAPH ?g {
    ?s ?p ?o .
  }
} WHERE {
  GRAPH ?g {
    ?s a <http://oscaf.sourceforge.net/ndo.html#DownloadEvent> ;
      dct:modified ?modified .
    FILTER (?modified < "2021-12-31"^^<http://www.w3.org/2001/XMLSchema#date>)

    ?s ?p ?o .
  }
}

;

# Delete tasks  older than a month, with containers, based on dct:modified

DELETE {
  GRAPH ?g {
    ?task ?taskP ?taskO.
    ?resultsContainer ?presultsContainer ?oresultsContainer .
    ?inputContainer ?pinputContainer ?oinputContainer .
  }
} WHERE {
  GRAPH ?g {
    VALUES ?operation {
      <http://lblod.data.gift/id/jobs/concept/JobOperation/berichtencentrumWarning>
      <http://lblod.data.gift/id/jobs/concept/JobOperation/downloadUrlWarning>
      <http://redpencil.data.gift/id/jobs/concept/JobOperation/deltas/healingOperation/mandatarissen>
      <http://redpencil.data.gift/id/jobs/concept/JobOperation/deltas/deltaDumpFileCreation/mandatarissen>
      <http://redpencil.data.gift/id/jobs/concept/JobOperation/deltas/deltaDumpFileCreation/leidinggevenden>
      <http://redpencil.data.gift/id/jobs/concept/JobOperation/deltas/healingOperation/leidinggevenden>
    }

    ?job a <http://vocab.deri.ie/cogs#Job> ;
      task:operation ?operation;
      dct:modified ?modified .

    FILTER (?modified < "2021-12-31"^^<http://www.w3.org/2001/XMLSchema#date>)

    ?job ?jobP ?jobO .
    ?task dct:isPartOf ?job.
    ?task ?taskP ?taskO.

    OPTIONAL {
      ?task task:resultsContainer ?resultsContainer .
      ?resultsContainer ?presultsContainer ?oresultsContainer .
    }

    OPTIONAL {
      ?task task:inputContainer ?inputContainer .
      ?inputContainer ?pinputContainer ?oinputContainer .
    }

  }
}

;

# Delete jobs, based on dct:modified

DELETE {
  GRAPH ?g {
    ?job ?jobP ?jobO
  }
} WHERE {
  GRAPH ?g {
    VALUES ?operation {
      <http://lblod.data.gift/id/jobs/concept/JobOperation/berichtencentrumWarning>
      <http://lblod.data.gift/id/jobs/concept/JobOperation/downloadUrlWarning>
      <http://redpencil.data.gift/id/jobs/concept/JobOperation/deltas/healingOperation/mandatarissen>
      <http://redpencil.data.gift/id/jobs/concept/JobOperation/deltas/deltaDumpFileCreation/mandatarissen>
      <http://redpencil.data.gift/id/jobs/concept/JobOperation/deltas/deltaDumpFileCreation/leidinggevenden>
      <http://redpencil.data.gift/id/jobs/concept/JobOperation/deltas/healingOperation/leidinggevenden>
    }

    ?job a <http://vocab.deri.ie/cogs#Job> ;
      task:operation ?operation;
      dct:modified ?modified .

    FILTER (?modified < "2021-12-31"^^<http://www.w3.org/2001/XMLSchema#date>)

    ?job ?jobP ?jobO .
  }
}

;

# Delete older than a month, based on dct:created

DELETE {
  GRAPH ?g {
    ?s ?p ?o .
  }
} WHERE {
  GRAPH ?g {
    ?s a ?type ;
      dct:created ?created .

    FILTER (?type IN (
      <http://open-services.net/ns/core#Error>,
      <http://mu.semte.ch/vocabularies/ext/export/Export>
    ))
    FILTER (?created < "2021-12-31"^^<http://www.w3.org/2001/XMLSchema#date>)

    ?s ?p ?o .
  }
}

;

# Delete older than a month, based on nmo:sentDate

DELETE {
  GRAPH ?g {
    ?s ?p ?o .
  }
} WHERE {
  GRAPH ?g {
    ?s a <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#Email> ;
      nmo:sentDate ?sentDate .

    FILTER (?sentDate < "2021-12-31"^^<http://www.w3.org/2001/XMLSchema#date>)

    ?s ?p ?o .
  }
}

;

# Delete older than a month, based on pav:createdOn

DELETE {
  GRAPH ?g {
    ?s ?p ?o .
  }
} WHERE {
  GRAPH ?g {
    ?s a <http://mu.semte.ch/vocabularies/ext/KalliopeSyncError> ;
      pav:createdOn ?createdOn .

    FILTER (?createdOn < "2021-12-31"^^<http://www.w3.org/2001/XMLSchema#date>)

    ?s ?p ?o .
  }
}

;

# Delete older than a month, based on rlog:date

DELETE {
  GRAPH ?g {
    ?s ?p ?o .
  }
} WHERE {
  GRAPH ?g {
    ?s a <http://persistence.uni-leipzig.org/nlp2rdf/ontologies/rlog#Entry> ;
      rlog:date ?date .

    FILTER (?date < "2021-12-31"^^<http://www.w3.org/2001/XMLSchema#date>)

    ?s ?p ?o .
  }
}

;

# Delete sessions older than a month. They don't have a type, so check based on their URI

DELETE {
  GRAPH ?g {
    ?s ?p ?o .
  }
} WHERE {
  GRAPH ?g {
    ?s dct:modified ?modified .

    FILTER NOT EXISTS { ?s a ?type . }
    FILTER (strstarts(str(?s), 'http://mu.semte.ch/sessions/'))
    FILTER (?modified < "2021-12-31"^^<http://www.w3.org/2001/XMLSchema#date>)

    ?s ?p ?o .
  }
}

;