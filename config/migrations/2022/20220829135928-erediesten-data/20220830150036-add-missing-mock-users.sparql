PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>

INSERT {
  GRAPH ?orgGraph {
    ?persoon a foaf:Person;
      mu:uuid ?uuidPersoon;
      foaf:firstName ?classificatie;
      foaf:familyName ?naam;
      foaf:member ?bestuurseenheid ;
      foaf:account ?account.
    ?account a foaf:OnlineAccount;
      mu:uuid ?uuidAccount;
      foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
      ext:sessionRole "LoketLB-toezichtGebruiker",
        "LoketLB-LPDCGebruiker",
        "LoketLB-eredienstBedienaarGebruiker",
        "LoketLB-eredienstMandaatGebruiker".
  }
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?persoon a foaf:Person;
      mu:uuid ?uuidPersoon;
      foaf:firstName ?classificatie;
      foaf:familyName ?naam;
      foaf:member ?bestuurseenheid ;
      foaf:account ?account.
    ?account a foaf:OnlineAccount;
      mu:uuid ?uuidAccount;
      foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
      ext:sessionRole "LoketLB-toezichtGebruiker",
        "LoketLB-LPDCGebruiker",
        "LoketLB-eredienstBedienaarGebruiker",
        "LoketLB-eredienstMandaatGebruiker".
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    VALUES ?type {
      erediensten:BestuurVanDeEredienst
      erediensten:CentraalBestuurVanDeEredienst
    }

    ?bestuurseenheid
      a ?type;
      mu:uuid ?uuidBestuurseenheid;
      skos:prefLabel ?naam;
      besluit:classificatie/skos:prefLabel ?classificatie.

    FILTER NOT EXISTS {
      ?persoon foaf:member ?bestuurseenheid .
    }

    BIND(CONCAT(?classificatie, " ", ?naam) as ?volledigeNaam)
    BIND(MD5(?volledigeNaam) as ?uuidPersoon)
    BIND(MD5(CONCAT(?volledigeNaam,"ACCOUNT")) as ?uuidAccount)
    BIND(IRI(CONCAT("http://data.lblod.info/id/persoon/", ?uuidPersoon)) AS ?persoon)
    BIND(IRI(CONCAT("http://data.lblod.info/id/account/", ?uuidAccount)) AS ?account)
    BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?uuidBestuurseenheid)) AS ?orgGraph)
  }
}
