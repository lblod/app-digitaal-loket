PREFIX cleanup: <http://mu.semte.ch/vocabularies/ext/cleanup/>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    <http://data.lblod.info/id/cleanup-job/4d3b2cec-68a7-4703-9cbc-0a8ca6c29539> cleanup:randomQuery ?randomQuery .
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    <http://data.lblod.info/id/cleanup-job/4d3b2cec-68a7-4703-9cbc-0a8ca6c29539> cleanup:randomQuery """
    DELETE {
      GRAPH ?g {
        ?resource <http://lblod.data.gift/vocabularies/security/targetAuthenticationConfiguration> ?targetAuthenticationConfiguration .
      }

      GRAPH ?h {
        ?resource <http://lblod.data.gift/vocabularies/security/targetAuthenticationConfiguration> ?targetAuthenticationConfiguration .
      }

      GRAPH ?i {
        ?targetAuthenticationConfiguration ?targetAuthenticationConfigurationP ?targetAuthenticationConfigurationO .
        ?targetAuthenticationConfigurationO ?p_t ?o_t .
      }
    }
    WHERE {
      GRAPH ?g {
        {
          VALUES ?status {
            <http://lblod.data.gift/file-download-statuses/success>
            <http://lblod.data.gift/file-download-statuses/failure>
          }

          ?resource a <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#RemoteDataObject> ;
            <http://www.w3.org/ns/adms#status> ?status ;
            <http://lblod.data.gift/vocabularies/security/targetAuthenticationConfiguration> ?targetAuthenticationConfiguration .
        }
        UNION
        {
          VALUES ?status {
            <http://redpencil.data.gift/id/concept/JobStatus/success>
            <http://redpencil.data.gift/id/concept/JobStatus/failed>
          }

          ?resource a <http://vocab.deri.ie/cogs#Job> ;
            <http://www.w3.org/ns/adms#status> ?status ;
            <http://lblod.data.gift/vocabularies/security/targetAuthenticationConfiguration> ?targetAuthenticationConfiguration .
        }
        UNION
        {
          VALUES ?status {
            <http://redpencil.data.gift/id/concept/JobStatus/success>
            <http://redpencil.data.gift/id/concept/JobStatus/failed>
          }

          ?job a <http://vocab.deri.ie/cogs#Job> ;
            <http://www.w3.org/ns/prov#generated> ?resource ;
            <http://www.w3.org/ns/adms#status> ?status .

          GRAPH ?h {
            ?resource a <http://rdf.myexperiment.org/ontologies/base/Submission> ;
              <http://lblod.data.gift/vocabularies/security/targetAuthenticationConfiguration> ?targetAuthenticationConfiguration .
          }
        }
      }

      OPTIONAL {
        GRAPH ?i {
          ?targetAuthenticationConfiguration ?targetAuthenticationConfigurationP ?targetAuthenticationConfigurationO .
          ?targetAuthenticationConfigurationO ?p_t ?o_t .
        }
      }
    }
    """
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    <http://data.lblod.info/id/cleanup-job/4d3b2cec-68a7-4703-9cbc-0a8ca6c29539> cleanup:randomQuery ?randomQuery .
  }
}
