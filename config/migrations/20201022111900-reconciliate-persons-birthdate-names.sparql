### Fix people having two birth dates

# From data examination we can see that some person have 3 birth dates, some have 2.

# Cleaning when 3 dates
# The "FILTER ?b1 < ?b2 ..." is here to ensure we only have one line per person, so that we don't accidentally 
# remove all the birthdates.

PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

DELETE {
  GRAPH ?g {
    ?person persoon:heeftGeboorte ?birthDateUri2 .
    ?person persoon:heeftGeboorte ?birthDateUri3 .
  }
} INSERT {
  GRAPH ?g {
    ?birthDateUri2 owl:sameAs ?birthDateUri1 .
    ?birthDateUri3 owl:sameAs ?birthDateUri1 .
  }
} WHERE {
  GRAPH ?g {
    ?person persoon:heeftGeboorte ?birthDateUri1, ?birthDateUri2, ?birthDateUri3 .
    FILTER(str(?birthDateUri1) < str(?birthDateUri2) AND str(?birthDateUri1) < str(?birthDateUri3))
    FILTER(str(?birthDateUri1) != str(?birthDateUri2))
    FILTER(str(?birthDateUri1) != str(?birthDateUri3))
    FILTER(str(?birthDateUri2) != str(?birthDateUri3))
  }
}

;

# Cleaning when 2 dates :

PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

DELETE {
  GRAPH ?h {
    ?person persoon:heeftGeboorte ?birthDateUri2 .
  }
} INSERT {
  GRAPH ?h {
    ?birthDateUri2 owl:sameAs ?birthDateUri1 .
  }
} WHERE {
  GRAPH ?g {
    ?person persoon:heeftGeboorte ?birthDateUri1 .
  }
  GRAPH ?h {
    ?person persoon:heeftGeboorte ?birthDateUri2 .
  }
  FILTER(str(?birthDateUri1) < str(?birthDateUri2))
  FILTER(str(?birthDateUri1) != str(?birthDateUri2))
}

;

### Fix people having two first names

PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>

DELETE {
  GRAPH ?h {
    ?person persoon:gebruikteVoornaam ?firstName2 .
  }
} WHERE {
  GRAPH ?g {
    ?person persoon:gebruikteVoornaam ?firstName1 .
  }
  GRAPH ?h {
    ?person persoon:gebruikteVoornaam ?firstName2 .
  }
  FILTER(str(?firstName1) < str(?firstName2))
  FILTER(str(?firstName1) != str(?firstName2))
}

;

### Fix people having two last names

PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

DELETE {
  GRAPH ?h {
    ?person foaf:familyName ?lastName2 .
  }
} WHERE {
  GRAPH ?g {
    ?person foaf:familyName ?lastName1 .
  }
  GRAPH ?h {
    ?person foaf:familyName ?lastName2 .
  }
  FILTER(str(?lastName1) < str(?lastName2))
  FILTER(str(?lastName1) != str(?lastName2))
}
