PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>

# Remove bestuurseenheid

DELETE {
  GRAPH ?g {
    <http://data.lblod.info/id/bestuurseenheden/bfb82c94-06f6-452d-b01b-959258636833> ?p ?o.
  }
}

WHERE {
  GRAPH ?g {
    <http://data.lblod.info/id/bestuurseenheden/bfb82c94-06f6-452d-b01b-959258636833> ?p ?o.
  }
}

;

# Remove bestuursorganen, bestuursorganen in tijd and bestuursfuncties

DELETE {
  GRAPH ?g {
    ?bestuursorgaan <http://data.vlaanderen.be/ns/besluit#bestuurt> <http://data.lblod.info/id/bestuurseenheden/bfb82c94-06f6-452d-b01b-959258636833> ;
      ?boP ?boO .
    ?bestuursorgaanInTijd <http://data.lblod.info/vocabularies/leidinggevenden/heeftBestuursfunctie> ?bestuursfunctie ;
      ?botP ?botO .
    ?bestuursfunctie ?beP ?beO .
  }
}

WHERE {
  GRAPH ?g {
    ?bestuursorgaan <http://data.vlaanderen.be/ns/besluit#bestuurt> <http://data.lblod.info/id/bestuurseenheden/bfb82c94-06f6-452d-b01b-959258636833> ;
      ?boP ?boO ;
      ^mandaat:isTijdspecialisatieVan ?bestuursorgaanInTijd .
    ?bestuursorgaanInTijd <http://data.lblod.info/vocabularies/leidinggevenden/heeftBestuursfunctie> ?bestuursfunctie ;
      ?botP ?botO .
    ?bestuursfunctie ?beP ?beO .
  }
}

;

DROP SILENT GRAPH <http://mu.semte.ch/graphs/organizations/bfb82c94-06f6-452d-b01b-959258636833/LoketLB-berichtenGebruiker>
