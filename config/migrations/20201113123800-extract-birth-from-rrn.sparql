PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

INSERT {
  GRAPH ?g {
    ?person persoon:heeftGeboorte ?newBirthDateUri .
    ?newBirthDateUri a <http://data.vlaanderen.be/ns/persoon#Geboorte> ;
      <http://mu.semte.ch/vocabularies/core/uuid> ?newBirthDateUuid ;
      persoon:datum ?newBirthDate .
  }
}
WHERE { 
  GRAPH ?g {
    ?person a person:Person ;
      adms:identifier ?identifier .

    ?identifier skos:notation ?rrn .

    OPTIONAL {
      ?person persoon:heeftGeboorte ?birthDateUri .
      ?birthDateUri persoon:datum ?birthDate .
    }
    FILTER ( NOT EXISTS { ?person persoon:heeftGeboorte ?birthDateUri . } )

    BIND ( SUBSTR(STR(?rrn), 1, 2) AS ?year ) .
    BIND ( SUBSTR(STR(?rrn), 3, 2) AS ?month ) .
    BIND ( SUBSTR(STR(?rrn), 5, 2) AS ?day ) .

    # Filtering out persons having undefined birth dates
    FILTER ( REGEX(STR(?year), "(?!00)[0-9]{2}") ) .
    FILTER ( REGEX(STR(?month), "(?!00)[0-9]{2}") ) .
    FILTER ( REGEX(STR(?day), "(?!00)[0-9]{2}") ) .

    BIND( MD5(CONCAT(?identifier,"BIRTHDATE")) as ?newBirthDateUuid ) .
    BIND( IRI(CONCAT("http://data.lblod.info/id/geboortes/", ?newBirthDateUuid)) AS ?newBirthDateUri ) .
    BIND( STRDT(CONCAT("19", ?year, "-", ?month, "-", ?day), xsd:date) as ?newBirthDate)
  }
}
