PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX nuao: <http://www.semanticdesktop.org/ontologies/2010/01/25/nuao#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX oauth: <http://kanselarij.vo.data.gift/vocabularies/oauth-2.0-session/>
PREFIX sec: <http://lblod.data.gift/vocabularies/security/>

DELETE {
  GRAPH ?g {
    ?submission ?sp ?so .
    ?submissionDocument ?sdp ?sdo .
    ?pFile ?pp ?po .
    ?formData ?fp ?fo .
    ?lFile ?lp ?lo .
    ?pFile2 ?p2p ?p2o .
    ?job ?jobp ?jobo .
    ?task ?taskp ?tasko .
    ?inputContainer ?icp ?ico .
    ?harvestingCollection ?hcp ?hco .
    ?rdo ?rdp ?rdpo .
    ?oldSubmissionAuthConfig ?ocacp ?osaco .
    ?oldSubmissionCreds ?oscp ?osco .
  }
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?dlevent ?dlp ?dlo .
  }
}
WHERE {
  BIND (<http://data.lblod.info/submissions/e6322070-4b5a-11f0-9e12-c339e04edc63> as ?submission)
  GRAPH ?g {
    {
      ?submission ?sp ?so .
    } UNION {
      ?submission dct:subject ?submissionDocument .
      ?submissionDocument ?sdp ?sdo .
    } UNION {
      ?submission dct:subject ?submissionDocument .
      ?submissionDocument dct:source ?pFile .
      ?pFile ?pp ?po .
    } UNION {
      ?submission prov:generated ?formData .
      ?formData ?fp ?fo .
    } UNION {
      ?submission prov:generated ?formData .
      ?formData dct:hasPart ?lFile .
      ?lFile ?lp ?lo .
    } UNION {
      ?submission prov:generated ?formData .
      ?formData dct:hasPart ?lFile .
      ?pFile2 nie:dataSource ?lFile .
      ?pFile2 ?p2p ?p2o .
    } UNION {
      ?job
        prov:generated ?submission ;
        ?jobp ?jobo .
      ?task
        task:inputContainer ?inputContainer ;
        dct:isPartOf ?job ;
        ?taskp ?tasko .
      ?inputContainer
        task:hasHarvestingCollection ?harvestingCollection ;
        ?icp ?ico .
      ?harvestingCollection
        dct:hasPart ?rdo ;
        ?hcp ?hco .
      ?rdo ?rdp ?rdpo .
    } UNION {
      ?submission sec:targetAuthenticationConfiguration ?oldSubmissionAuthConfig .
      ?oldSubmissionAuthConfig ?ocacp ?osaco .
      ?oldSubmissionAuthConfig sec:secrets ?oldSubmissionCreds .
      ?oldSubmissionCreds ?oscp ?osco .
    }
  }
  OPTIONAL {
    ?job prov:generated ?submission .
    ?task
      task:inputContainer ?inputContainer ;
      dct:isPartOf ?job .
    ?inputContainer task:hasHarvestingCollection ?harvestingCollection .
    ?harvestingCollection dct:hasPart ?rdo .
    GRAPH <http://mu.semte.ch/graphs/public> {
      ?dlevent nuao:involves ?rdo .
      ?dlevent ?dlp ?dlo .
    }
  }
}
