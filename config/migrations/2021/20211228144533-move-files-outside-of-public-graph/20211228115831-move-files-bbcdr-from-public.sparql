PREFIX schema: <http://schema.org/>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX bbcdr: <http://mu.semte.ch/vocabularies/ext/bbcdr/>

# moves the orig files
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bijlage a nfo:FileDataObject;
        <http://mu.semte.ch/vocabularies/core/uuid> ?vUuid;
        nfo:fileName ?vName;
        dct:format ?vMime;
        dct:created ?vCreated;
        nfo:fileSize ?vSize;
        dbpedia:fileExtension ?vExt.
    ?pFile a nfo:FileDataObject;
        <http://mu.semte.ch/vocabularies/core/uuid> ?pUuid;
        nfo:fileName ?pName;
        dct:format ?pMime;
        dct:created ?pCreated;
        nfo:fileSize ?pSize;
        dbpedia:fileExtension ?pExt;
        nie:dataSource ?bijlage.
  }
}
INSERT {
  GRAPH ?g {
    ?bijlage a nfo:FileDataObject;
        <http://mu.semte.ch/vocabularies/core/uuid> ?vUuid;
        nfo:fileName ?vName;
        dct:format ?vMime;
        dct:created ?vCreated;
        nfo:fileSize ?vSize;
        dbpedia:fileExtension ?vExt.
    ?pFile a nfo:FileDataObject;
        <http://mu.semte.ch/vocabularies/core/uuid> ?pUuid;
        nfo:fileName ?pName;
        dct:format ?pMime;
        dct:created ?pCreated;
        nfo:fileSize ?pSize;
        dbpedia:fileExtension ?pExt;
        nie:dataSource ?bijlage.
  }
}
WHERE {
  GRAPH ?g {
    ?report a bbcdr:Report;
      nie:hasPart ?bijlage.
  }
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bijlage a nfo:FileDataObject;
        <http://mu.semte.ch/vocabularies/core/uuid> ?vUuid;
        nfo:fileName ?vName;
        dct:format ?vMime;
        dct:created ?vCreated;
        nfo:fileSize ?vSize;
        dbpedia:fileExtension ?vExt.
    ?pFile a nfo:FileDataObject;
        <http://mu.semte.ch/vocabularies/core/uuid> ?pUuid;
        nfo:fileName ?pName;
        dct:format ?pMime;
        dct:created ?pCreated;
        nfo:fileSize ?pSize;
        dbpedia:fileExtension ?pExt;
        nie:dataSource ?bijlage.
  }
}

;

# moves the packaged files
DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bijlage a nfo:FileDataObject;
        <http://mu.semte.ch/vocabularies/core/uuid> ?vUuid;
        nfo:fileName ?vName;
        dct:format ?vMime;
        dct:created ?vCreated;
        dbpedia:fileExtension ?vExt.
  }
}
INSERT {
  GRAPH ?g {
    ?bijlage a nfo:FileDataObject;
        <http://mu.semte.ch/vocabularies/core/uuid> ?vUuid;
        nfo:fileName ?vName;
        dct:format ?vMime;
        dct:created ?vCreated;
        dbpedia:fileExtension ?vExt.
  }
}
WHERE {
  GRAPH ?g {
    ?report a bbcdr:Report;
      bbcdr:package ?bijlage.
  }
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bijlage a nfo:FileDataObject;
        <http://mu.semte.ch/vocabularies/core/uuid> ?vUuid;
        nfo:fileName ?vName;
        dct:format ?vMime;
        dct:created ?vCreated;
        dbpedia:fileExtension ?vExt.
  }
}