PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX pav: <http://purl.org/pav/>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX terms: <http://purl.org/dc/terms/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX transactie: <http://data.vlaanderen.be/ns/transactie#>

INSERT {
    GRAPH ?orggraph {
        ?participation a m8g:Participation ;
            mu:uuid ?uuidP ;
            m8g:role <http://lblod.data.gift/concepts/d8b8f3d1-7574-4baf-94df-188a7bd84a3a> . # APPLICANT

        ?organization m8g:playsRole ?participation .

        ?consumption a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?uuidC ;
            cpsv:follows ?flow ;
            common:active ?active ;
            transactie:isInstantieVan ?sm ;
            terms:created ?creator ;
            ext:lastModifiedBy ?modifier ;
            terms:created ?created ;
            terms:modified ?modified ;
            m8g:hasParticipation ?participation ;
            dct:source ?application .

        ?application
            dct:isPartOf ?active ;
            dct:source ?consumption .

    }
} WHERE {
    GRAPH ?orggraph {
        ?application a lblodSubsidie:ApplicationForm ;
            adms:status <http://lblod.data.gift/concepts/79a52da4-f491-4e2f-9374-89a13cde8ecd> ; # IN CONCEPT
            adms:status ?status ;
            lblodSubsidie:subsidyMeasure ?sm ;
            lblodSubsidie:timeBlock ?tb ;
            pav:createdBy ?organization ;
            terms:created ?creator ;
            ext:lastModifiedBy ?modifier ;
            terms:created ?created ;
            terms:modified ?modified .

        FILTER NOT EXISTS {
            ?application
                dct:isPartOf ?eas ;
                dct:source ?ec .
        }
    }
    GRAPH ?public {
        ?tb a lblodSubsidie:SubsidiemaatregelAanbodReeks ;
            lblodSubsidie:heeftSubsidieprocedurestap ?step ;
            common:active ?flow .
        ?active a lblodSubsidie:ApplicationStep ;
            terms:references ?step .
    }

    BIND(SHA256(CONCAT(STR(?application), "d37d4f9a-1e43-463d-8c18-c00e3e0d9754")) as ?uuidP) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/participations/", ?uuidP)) AS ?participation) .

    BIND(SHA256(CONCAT(STR(?application), "d37d4f9a-1e43-463d-8c18-c00e3e0d9754")) as ?uuidC) .
    BIND(IRI(CONCAT("http://data.lblod.info/id/subsidy-measure-consumptions/", ?uuidC)) AS ?consumption) .

}