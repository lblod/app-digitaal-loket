PREFIX mobiliteit: <https://data.vlaanderen.be/ns/mobiliteit#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xkos: <http://rdf-vocabulary.ddialliance.org/xkos#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX pav: <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>

####################################################################
### TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error
INSERT DATA {
    GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5> {
        <http://dummy/application/6c68539a-f6de-4488-8c1b-1c182e07a142> a lblodSubsidie:ApplicationForm ;
            dct:source <config://versions/20210329141908-contact-tracing-checkbox-multi-selection/form.ttl> ;
            dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/d461c5280b0cfbbb21fa68c2104c261ec53fa6647007d20ebb76aed5a2a2bbae> .
    }
}
;
#######################################
# UPDATE old forms for: Contact-tracing
DELETE {
    GRAPH ?g {
        ?form dct:source ?sources .
    }
} INSERT {
    GRAPH ?g {
        ?form dct:source <config://forms/contact-tracing/versions/20210420153600-initial-version/form.ttl>,
                         <config://forms/contact-tracing/versions/20210420153600-initial-version/form.json>,
                         <data://meta-files/initial/20210401111656-meta.ttl> .
    }
} WHERE {
    GRAPH ?g {
        VALUES ?spec {
            <config://versions/20210329141908-contact-tracing-checkbox-multi-selection/form.ttl>
        }
        ?form a lblodSubsidie:ApplicationForm ;
              dct:source ?spec ;
              dct:source ?sources ;
              dct:isPartOf ?afs .
    }
    GRAPH ?public {
        VALUES ?measure {
            <http://lblod.data.gift/concepts/2697fbe1-4226-4325-807b-5dfa58e40a95>
        }
        ?afs dct:isPartOf ?flow .
        ?flow xkos:belongsTo ?series .
        ?measure lblodSubsidie:heeftReeks ?series .
    }
}
;
######################################################################
# TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error
DELETE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}
WHERE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}