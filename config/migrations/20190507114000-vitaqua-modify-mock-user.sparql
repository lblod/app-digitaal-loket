PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

DELETE {
  GRAPH ?h {
    ?personToDelete ?pPerson ?oPerson .
    ?accountToDelete ?pAccount ?oAccount .
  }
}
INSERT {
  GRAPH ?g {
     ?persoon a foaf:Person;
           mu:uuid ?uuidPersoon;
           foaf:firstName ?classificatie;
           foaf:familyName ?naam;
           foaf:member <http://data.lblod.info/id/bestuurseenheden/8ceaf793863638005ae441d76e37b93a1c122bc5d112bec9c40cbaf9d6c0b890> ;
           foaf:account ?account.
     ?account a foaf:OnlineAccount;
           mu:uuid ?uuidAccount;
           foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
           ext:sessionRole "LoketLB-toezichtGebruiker", "LoketLB-bbcdrGebruiker", "LoketLB-mandaatGebruiker", "LoketLB-berichtenGebruiker".
  }
} WHERE {
     <http://data.lblod.info/id/bestuurseenheden/8ceaf793863638005ae441d76e37b93a1c122bc5d112bec9c40cbaf9d6c0b890> a besluit:Bestuurseenheid;
     skos:prefLabel ?naam;
     besluit:classificatie/skos:prefLabel ?classificatie.
     BIND(CONCAT(?classificatie, " ", ?naam) as ?volledigeNaam)
     BIND(MD5(?volledigeNaam) as ?uuidPersoon)
     BIND(MD5(CONCAT(?volledigeNaam,"ACCOUNT")) as ?uuidAccount)
     BIND(IRI(CONCAT("http://data.lblod.info/id/persoon/", ?uuidPersoon)) AS ?persoon)
     BIND(IRI(CONCAT("http://data.lblod.info/id/account/", ?uuidAccount)) AS ?account)

     VALUES ?g {
        <http://mu.semte.ch/graphs/organizations/8ceaf793863638005ae441d76e37b93a1c122bc5d112bec9c40cbaf9d6c0b890>
        <http://mu.semte.ch/graphs/public>
     }

     VALUES ?h {
       <http://mu.semte.ch/graphs/organizations/eac38ef4-6ba8-40a1-8c40-129f4ed30988>
       <http://mu.semte.ch/graphs/public>
     }

     GRAPH ?h {
       ?personToDelete a foaf:Person;
         ?pPerson ?oPerson ;
         foaf:member <http://data.lblod.info/id/bestuurseenheden/eac38ef4-6ba8-40a1-8c40-129f4ed30988> ;
         foaf:account ?accountToDelete .
       ?accountToDelete ?pAccount ?oAccount .
     }
}
