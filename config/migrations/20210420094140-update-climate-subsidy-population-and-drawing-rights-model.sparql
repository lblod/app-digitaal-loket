PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

DELETE {
  GRAPH ?h {
    ?bestuurseenheid
      lblodSubsidie:drawingRight ?drawingRight ;
      dbpedia:populationTotal ?populationTotal .
  }
}
INSERT {
  GRAPH ?h {
    <http://lblod.data.info/id/subsidy-measure-offers/64d40351-8128-464f-990f-41066154583e>
      m8g:hasCriterion ?criterion ;
      subsidie:biedtAan ?subsidyAmount ;
      lblodSubsidie:heeftReeks <http://lblod.data.info/id/subsidy-measure-offer-series/87b87f00-6f87-4412-be26-ea531220457c> .
    
    ?subsidyAmount a subsidie:Subsidiebebdrag ;
      mu:uuid ?subsidyAmountUuid ;
      subsidie:maximumbedrag ?drawingRight ;
      dct:requires ?requirementGroup .

    ?criterion a m8g:Criterium;
      mu:uuid ?criterionUuid ;
      dct:title "Toepasbaar op gemeentebesturen" ;
      m8g:fulfilledByRequirementGroup ?requirementGroup .

    ?requirementGroup a m8g:RequirementGroup ;
      mu:uuid ?requirementGroupUuid ;
      dct:description ?requirementGroupDescription ;
      m8g:hasCriterionRequirement ?criterionRequirementBestuurseenheid ;
      m8g:hasCriterionRequirement ?criterionRequirementPopulation .

    ?criterionRequirementBestuurseenheid a m8g:CriterionRequirement ;
      mu:uuid ?criterionRequirementBestuurseenheidUuid ;
      dct:description "Het gemeentebestuur van toepassing" ;
      lblodSubsidie:isSatisfiableBy ?bestuurseenheid .

    ?criterionRequirementPopulation a m8g:CriterionRequirement ;
      mu:uuid ?criterionRequirementPopulationUuid ;
      dct:description "Het vereiste bevolkingsaantal" ;
      dbpedia:populationTotal ?populationTotal .
  }
} WHERE {
  GRAPH ?g {
    ?bestuurseenheid a besluit:Bestuurseenheid ;
      skos:prefLabel ?labelBestuurseenheid .
  }
  GRAPH ?h {
    ?bestuurseenheid
      lblodSubsidie:drawingRight ?drawingRight ;
      dbpedia:populationTotal ?populationTotal .
  }

  BIND(SHA256(CONCAT(STR(?populationTotal), "subsidyAmount")) as ?subsidyAmountUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/subsidy-amounts/", ?subsidyAmountUuid)) AS ?subsidyAmount) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "criterion")) as ?criterionUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/criteria/", ?criterionUuid)) AS ?criterion) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "requirementGroup")) as ?requirementGroupUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/requirement-groups/", ?requirementGroupUuid)) AS ?requirementGroup) .
  BIND(CONCAT("Vereistengroup voor klimaatsubsidies - ", STR(?labelBestuurseenheid)) as ?requirementGroupDescription) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "criterionRequirementBestuurseenheid")) as ?criterionRequirementBestuurseenheidUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/criterion-requirements/", ?criterionRequirementBestuurseenheidUuid)) AS ?criterionRequirementBestuurseenheid) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "criterionRequirementPopulation")) as ?criterionRequirementPopulationUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/criterion-requirements/", ?criterionRequirementPopulationUuid)) AS ?criterionRequirementPopulation) .
}
